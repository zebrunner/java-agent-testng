tasks.register("publishSnapshotToNexus") {
    doFirst {
        if (!version.toString().endsWith("-SNAPSHOT")) {
            throw new GradleException("Version must end with -SNAPSHOT to publish snapshots.")
        }
    }
    dependsOn("publishMavenJavaPublicationToNexusRepository")
}

tasks.register("publishReleaseToCentral") {
    doFirst {
        if (version.toString().endsWith("-SNAPSHOT")) {
            throw new GradleException("Remove -SNAPSHOT for Central Portal releases.")
        }
    }
    dependsOn("publishMavenJavaPublicationToMavenCentral")
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = 'Zebrunner TestNG agent'
                description = 'Zebrunner TestNG agent library'
                url = 'https://github.com/zebrunner/java-agent-testng'
                organization {
                    name = 'Zebrunner'
                    url = 'https://zebrunner.com'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'sbrenko'
                        name = 'Sergey Brenko'
                        email = 'sbrenko@zebrunner.com'
                    }
                    developer {
                        id = 'dkazak'
                        name = 'Dmitry Kazak'
                        email = 'dkazak@solvd.com'
                    }
                }

                scm {
                    connection = 'https://github.com/zebrunner/java-agent-testng.git'
                    developerConnection = 'git@github.com:zebrunner/java-agent-testng.git'
                    url = 'https://github.com/zebrunner/java-agent-testng'
                }

                issueManagement {
                    system = 'GitHub'
                    url = 'https://github.com/zebrunner/java-agent-testng/issues'
                }
            }
        }
    }
    repositories {
        maven {
            name = 'nexus'
            url = project.findProperty('nexusUrl')
            credentials {
                username = project.findProperty('nexusUsername')
                password = project.findProperty('nexusPassword')
            }
        }
    }
}

signing {
    def signingKey = project.findProperty('signing.key') as String ?: System.getenv('SIGNING_KEY')
    def signingPassword = project.findProperty('signing.password') as String ?: System.getenv('SIGNING_PASSWORD')
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}
